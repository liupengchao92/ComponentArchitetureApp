if (Boolean.parseBoolean(isRelease)) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {

    compileSdk this.rootProject.android.compileSdkVersion

    defaultConfig {
        minSdk this.rootProject.android.minSdkVersion
        targetSdk this.rootProject.android.targetSdkVersion
        versionCode this.rootProject.android.versionCode
        versionName this.rootProject.android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (Boolean.parseBoolean(isRelease)) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                resources {
                    //合并打包版本时，排除debug文件夹下所有文件
                    exclude '*/debug/*'
                }
            } else {
                //模块化，作为独立App应用运行
                manifest.srcFile 'src/main/module_manifest/AndroidManifest.xml'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}